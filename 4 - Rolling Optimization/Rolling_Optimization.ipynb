{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Optimization code for PV microgrid:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "### Initialization:\n",
    "\n",
    "using JuMP, Gurobi, MATLAB\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "for my_int = 1:96\n",
    "    \n",
    "    #----------------------------------------------------------------------------------------------------\n",
    "    ### Solver Definition:\n",
    "    \n",
    "    m = Model(solver=GurobiSolver(Presolve=1,TimeLimit=600))\n",
    "        \n",
    "    #----------------------------------------------------------------------------------------------------\n",
    "    ### Loading Optimization Parameters:\n",
    "\n",
    "    mat\"generate_data($my_int)\"\n",
    "    \n",
    "    vars = read_matfile(\"../../Data/Generated Data/5 - Optimization/scenarios/scen_\"*dec(my_int)*\".mat\")\n",
    "\n",
    "    interval  = Int64(jvalue(vars[\"interval\"]))  # Current Interval (in day, for which we are optimizing)\n",
    "\n",
    "    N_EMS   = Int64(jvalue(vars[\"N_EMS\"]))   # num of remaining EMS   intervals (opt. executions)\n",
    "    N_intra = Int64(jvalue(vars[\"N_intra\"])) # num of remaining Intra intervals (opt. resolution)\n",
    "    N_dies  = Int64(jvalue(vars[\"N_dies\"]))  # num of diesel generators\n",
    "    N_scen  = Int64(jvalue(vars[\"N_scen\"]))  # num of different scenations\n",
    "    N_firm  = Int64(jvalue(vars[\"N_firm\"]))  # num of FIRM Margin Constraint Intervals\n",
    "\n",
    "    # Upper and Lower bounds:\n",
    "    P_bat_min = jvalue(vars[\"P_bat_min\"])\n",
    "    P_bat_max = jvalue(vars[\"P_bat_max\"])\n",
    "    eta_bat  = jvalue(vars[\"eta_bat\"])\n",
    "    SOC_min  = jvalue(vars[\"SOC_min\"])\n",
    "    SOC_max  = jvalue(vars[\"SOC_max\"])\n",
    "    SOC_init = jvalue(vars[\"SOC_init\"])\n",
    "    Cap_bat  = jvalue(vars[\"Cap_bat\"])\n",
    "    P_dies_max = jvalue(vars[\"P_dies_max\"])\n",
    "    P_dies_min = jvalue(vars[\"P_dies_min\"])\n",
    "    P_PV_inst = jvalue(vars[\"P_PV_inst\"])\n",
    "\n",
    "    delta_t = jvalue(vars[\"delta_t\"]) # 1 min\n",
    "\n",
    "    # f_min = -100000\n",
    "    f_min = jvalue(vars[\"f_min\"])\n",
    "    theta_i = jvalue(vars[\"theta_i\"])\n",
    "    theta_d = jvalue(vars[\"theta_d\"])\n",
    "    theta_b = jvalue(vars[\"theta_b\"])\n",
    "    theta_p = jvalue(vars[\"theta_p\"])\n",
    "\n",
    "    marge_dies = jvalue(vars[\"marge_dies\"])\n",
    "\n",
    "    L_PV = jvalue(vars[\"L_PV\"]) # generated PV scenarios\n",
    "    L_C  = jvalue(vars[\"L_C\"]) # generated load scenarios\n",
    "\n",
    "    # Defining iterators:\n",
    "    ts  = 1:N_EMS\n",
    "    ps  = 1:N_intra\n",
    "    ds  = 1:N_dies\n",
    "    ss  = 1:N_scen\n",
    "    m1s = 1:N_firm\n",
    "    m2s = (N_firm+1):N_EMS\n",
    "    \n",
    "    #----------------------------------------------------------------------------------------------------\n",
    "    ### Variable definitions:\n",
    "\n",
    "    @variables m begin\n",
    "\n",
    "        # Battery variables:\n",
    "        P_bat_min <= P_bat_set[t=ts,p=ps]  <= P_bat_max # Battery setpoint for time t,p (decision var.)\n",
    "        0 <= P_bat_cha[t=ts,p=ps] <= P_bat_max\n",
    "        0 <= P_bat_dis[t=ts,p=ps] <= P_bat_max\n",
    "        X_bat[t=ts,p=ps], Bin                           # Operating mode indicator (decision var.)\n",
    "                                                        # -> P_bat_set = 2*(X_bat-0.5)*P_bat_abs\n",
    "        SOC_min <= SOC_bat[t=ts,p=ps] <= SOC_max        # Obtained SOC for time t,p (solution var.)\n",
    "\n",
    "        # PV Plant:\n",
    "        0 <= P_PV_set[t=ts,p=ps]  <= P_PV_inst          # PV setpoint variable (decision var.)\n",
    "        0 <= P_PV[t=ts,p=ps,s=ss] <= L_PV[t,p,s]        # Actual PV generation (solution var.)\n",
    "\n",
    "        # Diesel Generators:\n",
    "        ON_dies[t=ts,d=ds], Bin                         # Dies. connectin setpoint (decision var.)\n",
    "        0 <= P_dies[t=ts,p=ps,s=ss,d=ds] <= P_dies_max  # Actual dies. generation  (solution var.)\n",
    "        # P_dies_min <= P_dies[t=ts,p=ps,s=ss,d=ds] <= P_dies_max # Actual dies. generation  (solution var.)\n",
    "\n",
    "    end\n",
    "    \n",
    "    #----------------------------------------------------------------------------------------------------\n",
    "    ### Variable Initialization (from previous optimization)\n",
    "    \n",
    "    if my_int>1\n",
    "\n",
    "        vars = read_matfile(\"../../Data/Generated Data/5 - Optimization/solutions/sol_\"*dec(my_int-1)*\".mat\")\n",
    "\n",
    "        P_bat_set_pre = jvalue(vars[\"P_bat_set\"])[2:N_EMS,ps]\n",
    "        SOC_bat_pre   = jvalue(vars[\"SOC_bat\"])[2:N_EMS,ps]\n",
    "        P_PV_set_pre  = jvalue(vars[\"P_PV_set\"])[2:N_EMS,ps]\n",
    "        P_PV_pre      = jvalue(vars[\"P_PV\"])[2:N_EMS,ps,ss]\n",
    "        ON_dies_pre   = jvalue(vars[\"ON_dies\"])[2:N_EMS,ds]\n",
    "        P_dies_pre    = jvalue(vars[\"P_dies\"])[2:N_EMS,ps,ss,ds]\n",
    "        P_bat_cha_pre = jvalue(vars[\"P_bat_cha\"])[2:N_EMS,ps]\n",
    "        P_bat_dis_pre = jvalue(vars[\"P_bat_dis\"])[2:N_EMS,ps]\n",
    "        X_bat_pre     = jvalue(vars[\"X_bat\"])[2:N_EMS,ps]\n",
    "\n",
    "        setvalue(P_bat_set[1:(N_EMS-1),ps],P_bat_set_pre)\n",
    "        setvalue(SOC_bat[1:(N_EMS-1),ps],SOC_bat_pre)\n",
    "        setvalue(P_PV_set[1:(N_EMS-1),ps],P_PV_set_pre)\n",
    "        setvalue(P_PV[1:(N_EMS-1),ps,ss],P_PV_pre)\n",
    "        setvalue(ON_dies[1:(N_EMS-1),ds],ON_dies_pre)\n",
    "        setvalue(P_dies[1:(N_EMS-1),ps,ss,ds],P_dies_pre)\n",
    "        setvalue(P_bat_cha[1:(N_EMS-1),ps],P_bat_cha_pre)\n",
    "        setvalue(P_bat_dis[1:(N_EMS-1),ps],P_bat_dis_pre)\n",
    "        setvalue(X_bat[1:(N_EMS-1),ps],X_bat_pre)\n",
    "\n",
    "    end\n",
    "    \n",
    "    #----------------------------------------------------------------------------------------------------\n",
    "    ### Constraints introduction:\n",
    "    \n",
    "    @constraints m begin\n",
    "\n",
    "        # Constraints:\n",
    "        #\n",
    "        # A1 - Diesel power limitation\n",
    "        # A2 - Diesel power limitation\n",
    "        # B1 - Power balance equation\n",
    "        # C1 - PV setpoint limitation\n",
    "        # D1 - Bat. setpoint equation\n",
    "        # D2 - Charging    mode const.\n",
    "        # D3 - Discharging mode const.\n",
    "        # E1 - FIRM    Diesel reserve margin (separated over scenarios)\n",
    "        # E2 - RELAXED Diesel reserve margin (mean over scenarios)\n",
    "        # F1 - \n",
    "        # F2 - SOC limitations\n",
    "        # F3 - \n",
    "        # G1 - Min frequency limitation\n",
    "\n",
    "        A1[t=ts,p=ps,s=ss,d=ds], P_dies[t,p,s,d] <= ON_dies[t,d]*(P_dies_max)\n",
    "        A2[t=ts,p=ps,s=ss,d=ds], P_dies[t,p,s,d] >= ON_dies[t,d]*(P_dies_min)\n",
    "\n",
    "        B1[t=ts,p=ps,s=ss], (P_PV[t,p,s] + sum(P_dies[t,p,s,d] for d=ds) + P_bat_set[t,p] - L_C[t,p,s]) == 0\n",
    "\n",
    "        C1[t=ts,p=ps,s=ss],  P_PV[t,p,s] <= P_PV_set[t,p]\n",
    "\n",
    "        D1[t=ts,p=ps], P_bat_set[t,p] == P_bat_cha[t,p] - P_bat_dis[t,p]\n",
    "        D2[t=ts,p=ps], P_bat_cha[t,p] <= P_bat_max *    X_bat[t,p]\n",
    "        D3[t=ts,p=ps], P_bat_dis[t,p] <= P_bat_max * (1-X_bat[t,p])\n",
    "\n",
    "        # E1[t=ts,p=ps,s=ss],  sum(P_dies[t,p,s,d] for d=ds) <= sum((ON_dies[t,d]*P_dies_max) for d=ds) - marge_dies\n",
    "        E1[t=m1s,p=ps,s=ss],  sum(P_dies[t,p,s,d] for d=ds) <= sum((ON_dies[t,d]*P_dies_max) for d=ds) - marge_dies\n",
    "        E2[t=m2s,p=ps],  sum(P_dies[t,p,s,d] for d=ds,s=ss)/N_scen <= sum((ON_dies[t,d]*P_dies_max) for d=ds) - marge_dies\n",
    "\n",
    "        F1[t=1,p=1],               SOC_bat[t,p] == SOC_init             - P_bat_set[t,p]*delta_t/Cap_bat\n",
    "        F2[t=2:N_EMS,p=1],         SOC_bat[t,p] == SOC_bat[t-1,N_intra] - P_bat_set[t,p]*delta_t/Cap_bat\n",
    "        F3[t=1:N_EMS,p=2:N_intra], SOC_bat[t,p] == SOC_bat[t,p-1]       - P_bat_set[t,p]*delta_t/Cap_bat\n",
    "\n",
    "        G1[t=ts,p=ps], f_min <= theta_i + theta_d*sum(ON_dies[t,d] for d=ds) + theta_b*P_bat_set[t,p] + theta_p*P_PV_set[t,p]\n",
    "\n",
    "    end\n",
    "\n",
    "    #----------------------------------------------------------------------------------------------------\n",
    "    ### Objective function definition:\n",
    "    \n",
    "    @objective(m, Max, sum((sum(P_PV[t,p,s] for s=ss)-N_scen*(1-eta_bat)*(P_bat_cha[t,p]+P_bat_dis[t,p])) for t=ts,p=ps));\n",
    "    \n",
    "    #----------------------------------------------------------------------------------------------------\n",
    "    ### Solving Optimization:\n",
    "    status = solve(m)\n",
    "\n",
    "    #----------------------------------------------------------------------------------------------------\n",
    "    ### Saving Data:\n",
    "    \n",
    "    write_matfile(\"../../Data/Generated Data/5 - Optimization/solutions/sol_\"*dec(interval)*\".mat\"; \n",
    "        status    = string(status),\n",
    "        interval  = interval,\n",
    "        P_bat_set = getvalue(P_bat_set[:,:]), \n",
    "        SOC_bat   = getvalue(SOC_bat[:,:]),\n",
    "        P_PV_set  = getvalue(P_PV_set[:,:]),\n",
    "        P_PV      = getvalue(P_PV[:,:,:]),\n",
    "        ON_dies   = getvalue(ON_dies[:,:]),\n",
    "        P_dies    = getvalue(P_dies[:,:,:,:]),\n",
    "        P_bat_cha = getvalue(P_bat_cha[:,:]),\n",
    "        P_bat_dis = getvalue(P_bat_dis[:,:]),\n",
    "        X_bat     = getvalue(X_bat[:,:]))\n",
    "    \n",
    "    # IJulia.clear_output(wait=false)\n",
    "    println(\"Current progress: \"*dec(my_int)*\"/144\")\n",
    "    \n",
    "    if string(status)==\"Infeasible\"\n",
    "        break\n",
    "    end\n",
    "    \n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.0",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
